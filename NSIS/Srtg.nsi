; Script generated by the HM NIS Edit Script Wizard.

;!define ARCH64

!include "LogicLib.nsh"
!include "x64.nsh"
!include "redist-net45.nsh"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "SRTG"
!define PRODUCT_VERSION "0.1.1"
!define PRODUCT_PUBLISHER "Beebop"
!define PRODUCT_WEB_SITE "http://beebop.fr"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Srtg.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; Config file extension
!define EXT_STR ".srtg"
!define EXT_DESC "SRTG Configuration File"

!ifdef ARCH64
  !define COMPILEDIR "..\Srtg\bin\x64\Release"
!else
  !define COMPILEDIR "..\Srtg\bin\Release"
!endif

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
;!insertmacro MUI_PAGE_LICENSE "..\..\..\..\..\..\dossier\vers\license\VotreLicenseLogicielle.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\Srtg.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "French"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
!ifdef ARCH64
  OutFile "Srtg_${PRODUCT_VERSION}_x64_Installer.exe"
!else
  OutFile "Srtg_${PRODUCT_VERSION}_Installer.exe"
!endif

!ifdef ARCH64
  InstallDir "$PROGRAMFILES64\Srtg"
  InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" "x64"
!else
  InstallDir "$PROGRAMFILES\Srtg"
  InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" "x86"
!endif

ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  !ifdef ARCH64
    ; Check if x64 system or not
    ${IfNot} ${RunningX64}
      MessageBox MB_OK "This installer must be used on 64 bits systems only."
      Abort
    ${EndIf}
  !endif
  
  !insertmacro MUI_LANGDLL_DISPLAY
	
	Call REDIST_NET45_Check
  ${If} $REDIST_NET45_INSTALLED == "No"
    MessageBox MB_OK|MB_ICONINFORMATION "${PRODUCT_NAME} require .NET Framework 4.5.2.$\n.NET 4.5.2 will be downloaded and installed during setup."
  ${EndIf}
	
FunctionEnd

Section "SectionPrincipale" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite try
	
	; .NET installation
  ${If} $REDIST_NET45_INSTALLED == "No"
    DetailPrint ".NET Framework 4.5.2 is missing and will be installed"
    Call REDIST_NET45_Install
  ${EndIf}
	
  File "${COMPILEDIR}\SnmpSharpNet.dll"
  File "${COMPILEDIR}\Srtg.exe"
  CreateDirectory "$SMPROGRAMS\Srtg"
  CreateShortCut "$SMPROGRAMS\Srtg\SRTG.lnk" "$INSTDIR\Srtg.exe"
  CreateShortCut "$DESKTOP\SRTG.lnk" "$INSTDIR\Srtg.exe"
  File "..\Srtg\bin\Release\WpfAnimatedGif.dll"
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\Srtg\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -RegisterFileExtension
  WriteRegStr HKCR "${EXT_STR}" "" "${EXT_DESC}"
  WriteRegStr HKCR "${EXT_DESC}" "" "${EXT_DESC}"
  WriteRegStr HKCR "${EXT_DESC}\shell" "" "open"
  WriteRegStr HKCR "${EXT_DESC}\DefaultIcon" "" "$INSTDIR\Srtg.exe,0"
  WriteRegStr HKCR "${EXT_DESC}\shell\open\command" "" '"$INSTDIR\Srtg.exe" "%1"'
  WriteRegStr HKCR "${EXT_DESC}\shell\edit" "" "Open with SRTG"
  WriteRegStr HKCR "${EXT_DESC}\shell\edit\command" "" '"$INSTDIR\Srtg.exe" "%1"'
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Srtg.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\Srtg.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) a été désinstallé avec succès de votre ordinateur."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Êtes-vous certains de vouloir désinstaller totalement $(^Name) et tous ses composants ?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\WpfAnimatedGif.dll"
  Delete "$INSTDIR\Srtg.exe"
  Delete "$INSTDIR\SnmpSharpNet.dll"

  Delete "$SMPROGRAMS\Srtg\Uninstall.lnk"
  Delete "$DESKTOP\SRTG.lnk"
  Delete "$SMPROGRAMS\Srtg\SRTG.lnk"

  RMDir "$SMPROGRAMS\Srtg"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  
  DeleteRegKey HKCR "${EXT_STR}"
  DeleteRegKey HKCR "${EXT_DESC}"
  
  SetAutoClose true
SectionEnd